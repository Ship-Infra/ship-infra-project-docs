provider "aws" {
  region                   = "us-east-1"
  shared_credentials_files = ["./.aws-credentials"]
  profile                  = "terraform"
}

resource "aws_route53_zone" "main" {
  name = "ship-infra.com"
}

resource "aws_route53domains_registered_domain" "main" {
  domain_name = "ship-infra.com"

  # Current Name Servers
  dynamic "name_server" {
    for_each = aws_route53_zone.main.name_servers
    content {
      name = name_server.value
    }
  }

  # Vercel Nameservers
  name_server {
    name = "ns1.vercel-dns.com"
  }

  name_server {
    name = "ns2.vercel-dns.com"
  }
}

variable "vercel_cname_records" {
  type = map(string)
  default = {
    www          = "ef382456d0a65bde.vercel-dns-017.com"
  }
}

resource "aws_route53_record" "vercel_cname" {
  for_each = var.vercel_cname_records

  zone_id = aws_route53_zone.main.zone_id
  name    = each.key
  type    = "CNAME"
  ttl     = 300
  records = [each.value]
}

variable "google_search_console_verification" {
  type    = map(string)
  default = {
    "ship-infra.com" = "google-site-verification=37fahvmO36jDe089m5kdquBka3CODgvP11Gbj1x1QyA"
  }
}

resource "aws_route53_record" "google_txt_verification" {
  for_each = var.google_search_console_verification

  zone_id = aws_route53_zone.main.zone_id
  name    = each.key
  type    = "TXT"
  ttl     = 300
  records = [each.value]
}
